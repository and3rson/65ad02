.zeropage

KBD_CNT: .res 1
KBD_SCA: .res 1
KBD_RDY: .res 1
KBD_FLAGS: .res 1  ; 7..3=n/c, 2=SHIFT, 1=EXT, 0=BREAK
KBD_FLAG_BREAK = $1
KBD_FLAG_EXT = $2
KBD_FLAG_SHIFT = $4
KBD_CHR: .res 1

KBD_SCA_EXT     =  $E0
KBD_SCA_BREAK   =  $F0
KBD_SCA_LSHIFT  =  $12
KBD_SCA_RSHIFT  =  $59

.code

KEYMAP:
    .byte "????????????? `?" ; 00-0F
    .byte "?????q1???zsaw2?" ; 10-1F
    .byte "?cxde43?? vftr5?" ; 20-2F
    .byte "?nbhgy6???mju78?" ; 30-3F
    .byte "?,kio09??./l;p-?" ; 40-4F
    .byte "??'?[=????",$0a,"]?\\??" ; 50-5F
    .byte "??????",$08,"??1?47???" ; 60-6F
    .byte "0.2568",$1b,"??+3-*9??" ; 70-7F
    .byte "????????????????" ; 80-8F
    .byte "????????????????" ; 90-9F
    .byte "????????????????" ; A0-AF
    .byte "????????????????" ; B0-BF
    .byte "????????????????" ; C0-CF
    .byte "????????????????" ; D0-DF
    .byte "????????????????" ; E0-EF
    .byte "????????????????" ; F0-FF
KEYMAP_SHIFTED:
    .byte "????????????? ~?" ; 00-0F
    .byte "?????Q!???ZSAW@?" ; 10-1F
    .byte "?CXDE#$?? VFTR%?" ; 20-2F
    .byte "?NBHGY^???MJU&*?" ; 30-3F
    .byte "?<KIO)(??>?L:P_?" ; 40-4F
    .byte "??",'"',"?{+?????}?|??" ; 50-5F
    .byte "?????????1?47???" ; 60-6F
    .byte "0.2568???+3-*9??" ; 70-7F
    .byte "????????????????" ; 80-8F
    .byte "????????????????" ; 90-9F
    .byte "????????????????" ; A0-AF
    .byte "????????????????" ; B0-BF
    .byte "????????????????" ; C0-CF
    .byte "????????????????" ; D0-DF
    .byte "????????????????" ; E0-EF
    .byte "????????????????" ; F0-FF

kbd_init:
        stz KBD_CNT
        stz KBD_SCA
        stz KBD_RDY
        stz KBD_FLAGS
        stz KBD_CHR

        rts

; Process PS/2 bit, update keyboard state
; Arguments:
;   A - 0 or 1
kbd_process:
        pha
        phx
        phy

        ldx KBD_CNT
        inx
        stx KBD_CNT

        ; Decide what to do with bit
        cpx #1
        beq @end  ; Start bit, ignore
        cpx #10
        beq @end  ; Parity bit, ignore
        cpx #11
        beq @complete  ; Frame complete

        ; Handle PS/2 bit as data
        ror A
        ror KBD_SCA
        jmp @end

    @complete:
        ; Frame finished
        stz KBD_CNT
        ldx KBD_SCA
        ; Check is scancode is BREAK
        cpx #KBD_SCA_BREAK
        bne @check_ext
        smb0 KBD_FLAGS
        jmp @end
    @check_ext:
        ; Check is scancode is EXT
        cpx #KBD_SCA_EXT
        bne @check_bat
        smb1 KBD_FLAGS
        jmp @end
    @check_bat:
        ; Ignore BAT
        cpx #$AA
        beq @end

        ; Load all flags, clear BREAK & EXT
        lda KBD_FLAGS
        rmb0 KBD_FLAGS
        rmb1 KBD_FLAGS
        ; If make/break shift - update shift flag
        cpx #KBD_SCA_LSHIFT
        beq @update_shift
        cpx #KBD_SCA_RSHIFT
        beq @update_shift
        tay
        and #KBD_FLAG_BREAK  ; Is break flag set?
        bne @end
        tya
        and #KBD_FLAG_SHIFT  ; Is shift flag set?
        bne @shifted

        ; Convert scancode into character
        lda KEYMAP, X
        jmp @print
    @shifted:
        ; Convert scancode into shifted character
        lda KEYMAP_SHIFTED, X
    @print:
        sta KBD_CHR
        lda #1
        sta KBD_RDY
        jmp @end

    @update_shift:
        and #KBD_FLAG_BREAK
        bne @clear_shift
        smb2 KBD_FLAGS  ; Set shift flat
        jmp @end
    @clear_shift:
        rmb2 KBD_FLAGS  ; Clear shift flag
        jmp @end

    @end:
        ply
        plx
        pla

        rts


; Wait for next character
; Return:
;   A - character ASCII code
kbd_getch:
    @again:
        lda KBD_RDY
        beq @again  ; Keyboard register not ready yet

        ; Keyboard register is ready
        sei
        stz KBD_RDY  ; Clear readiness flag
        lda KBD_CHR  ; Read register
        cli

        rts

